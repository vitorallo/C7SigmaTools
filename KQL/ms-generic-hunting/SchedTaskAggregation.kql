// Description: 'This query aggregates and charts cron job data based on unique user-command pairs. It shows the frequency of commands, the number of computers they've run on, and their percentage of total tenant computers.'
// Required Connectors: Syslog (Types: Syslog)

// Pull messages from Syslog-cron where the process name is "CRON" or "CROND", the severity level is info, and the SyslogMessage contains "CMD".
// It also parses out the user and commandline from the message.
let RawCommands = Syslog 
| where Facility =~ "cron" 
| where SeverityLevel =~ "info" 
| where ProcessName =~ "CRON" or ProcessName =~ "CROND"  
| where SyslogMessage contains "CMD " 
| project TenantId, TimeGenerated, Computer, SeverityLevel, ProcessName, SyslogMessage
| extend TrimmedSyslogMsg = trim_end(@"\)", SyslogMessage)
| parse TrimmedSyslogMsg with * "(" user  ") CMD (" cmdline 
| project TenantId, TimeGenerated, Computer, user, cmdline; 
// Count how many times a particular commandline has been seen based on unique Computer, User, and cmdline sets
let CommandCount = RawCommands
| summarize StartTime = min(TimeGenerated), EndTime = max(TimeGenerated), CmdlineCount = count() by Computer, user, cmdline; 
// Count how many computers have run a particular user and cmdline pair
let DistComputerCount = RawCommands
| summarize ComputerCount = dcount(Computer) by TenantId, user, cmdline; 
// Join above counts based on user and commandline pair
let CommandSummary = CommandCount | join (DistComputerCount) on user, cmdline
| project StartTime, EndTime, TenantId, user, CmdlineCount, ComputerCount, cmdline;
// Count the total number of computers reporting cron messages in the tenant
let TotalComputers = Syslog
| where Facility =~ "cron"
| summarize dcount(Computer) by TenantId ;
// Join the previous counts with the total computers count. Calculate the percentage of total computers value.
let FinalSummary = CommandSummary | join kind= leftouter (TotalComputers) on TenantId
| project StartTime, EndTime, user, TimesCmdlineSeen = CmdlineCount, CompsThatHaveRunCmdline = ComputerCount, 
AsPercentOfTotalComps = round(100 * (toreal(ComputerCount)/toreal(dcount_Computer)),2), cmdline
| order by user asc, TimesCmdlineSeen desc;
FinalSummary 
| extend timestamp = StartTime, AccountCustomEntity = user