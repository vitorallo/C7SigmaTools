// Description: 'Shows all users from a given department, which have a high impact on the organization, who connected to a resource for the first time and none of their peers accessed it.'
// Required Connectors: BehaviorAnalytics (Types: BehaviorAnalytics, IdentityInfo)

let Group = 'Helpdesk'; // Update this value as per your requirement
IdentityInfo
| summarize arg_max(TimeGenerated, *) by AccountObjectId //get latest information
| where Department =~ Group
| where BlastRadius =~ 'High'
| join kind=inner ( BehaviorAnalytics
         | where ActivityType =~ 'LogOn'
         | where ActionType =~ 'ResourceAccess' //filter for specific activties
         | where ActivityInsights.FirstTimeUserAccessedResource == True
         | where ActivityInsights.ResourceUncommonlyAccessedAmongPeers == True )
on $left.AccountUPN == $right.UserPrincipalName
| extend AadUserId = UsersInsights.AccountObjectID
| extend Account_0_AadUserId = AadUserId