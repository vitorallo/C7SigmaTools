// Description: 'Adversaries may steal the credentials of a specific user or service account using credential access techniques or capture credentials earlier in their reconnaissance process through social engineering as a means of gaining persistence." Umbreon, for example, creates valid users to provide access to the system.
// The query below generates an output of successful sign-in with one or more of the following indications:-
// - performed by new or recently dormant accounts
// - where one or more features of the activity deviate from the user, his peers, or the tenant's profile.
// - performed by a user with risk information from Microsoft Entra ID'
// Required Connectors: BehaviorAnalytics (Types: BehaviorAnalytics); AzureActiveDirectory (Types: SigninLogs)

BehaviorAnalytics
| where ActionType =~ "Sign-in"
| where UsersInsights.IsNewAccount == True or UsersInsights.IsDormantAccount == True or ActivityInsights has "True"
| join kind = inner (
SigninLogs | where  (Status.errorCode == 0 and RiskDetail !~ "none")
) on $left.SourceRecordId == $right._ItemId 
| extend UserPrincipalName = iff(UserPrincipalName has "#EXT#",replace_string(tostring(split(UserPrincipalName, "#")[0]),"_","@"),UserPrincipalName),
UserName = iff(UserName has "#EXT#",replace_string(tostring(split(UserPrincipalName, "#")[0]),"_","@"),UserName)
| project TimeGenerated, UserName, UserPrincipalName, UsersInsights, ActivityType, ActionType, ["Evidence"]=ActivityInsights, ResourceDisplayName, AppDisplayName, SourceIPAddress, SourceIPLocation, SourceDevice, DevicesInsights, ResourceId
| extend Name=tostring(split(UserPrincipalName, "@")[0]), UPNSuffix=tostring(split(UserPrincipalName, "@")[1])
| extend Account_0_Name = Name
| extend Account_0_UPNSuffix = UPNSuffix
| extend IP_0_Address = SourceIPAddress
| extend AzureResource_0_ResourceId = ResourceId