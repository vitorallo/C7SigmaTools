// Description: 'This query displays user edits to scheduled tasks via crontab, bucketed into 10-min intervals. It collects all actions by a user over seven days.'
// Required Connectors: Syslog (Types: Syslog); SyslogAma (Types: Syslog)

// Pull messages from Syslog-cron logs where the process is crontab and the severity level is "info". Extract the User and Action information from the SyslogMessage
Syslog 
| where Facility =~ "cron" 
| where ProcessName =~ "crontab" 
| where SeverityLevel =~ "info" 
| project TimeGenerated, Computer, SeverityLevel, ProcessName, SyslogMessage
| parse SyslogMessage with * "(" user  ") " Action " (" *
// Only look for messages that contain edit or replace
| where Action contains "EDIT" or Action contains "REPLACE"
//| summarize all the actions into a single set based on 10 minute time intervals
| summarize ListOfActions = makeset(Action) by EventTime10MinInterval = bin(TimeGenerated, 10m), Computer, user   
| order by Computer asc nulls last, EventTime10MinInterval asc
| extend timestamp = EventTime10MinInterval, AccountCustomEntity = user, HostCustomEntity = Computer