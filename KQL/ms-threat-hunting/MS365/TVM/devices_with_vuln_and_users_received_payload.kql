// Description: // Author: jan geisbauer
// // @janvonkirchheim
// // ------------------------
// // 1.	A list of all devices that have this vulnerability
// // 2.	A list of all users that uses those devices
// // 3.	If these users received .mkv files recently
// Required Connectors: MicrosoftThreatProtection (Types: DeviceTvmSoftwareVulnerabilities, DeviceInfo, IdentityInfo, EmailAttachmentInfo)

let all_computers_with_vlcvln=
DeviceTvmSoftwareVulnerabilities 
| where SoftwareName contains "vlc" 
| summarize makelist(DeviceName, 200);
let all_affected_users=
DeviceInfo
| where DeviceName in (all_computers_with_vlcvln)
| mvexpand todynamic(LoggedOnUsers)
| extend ParsedFields = parsejson(LoggedOnUsers)
| project UserName = ParsedFields.UserName
| summarize makelist(tolower(UserName), 200);
let all_email_addresses_aff_users=
IdentityInfo
| where tolower(AccountName) in (all_affected_users)
| summarize makelist(tolower(MailAddress), 200);
EmailAttachmentInfo
| where FileName contains ".mkv"
| where tolower(RecipientEmailAddress) in (all_email_addresses_aff_users)