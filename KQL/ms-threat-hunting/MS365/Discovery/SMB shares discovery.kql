// Description: Query for processes that accessed more than 10 IP addresses over port 445 (SMB) - possibly scanning for network shares.
// To read more about Network Share Discovery, see: https://attack.mitre.org/wiki/Technique/T1135.
// Tags: #SMB, #NetworkScanning, #UniqueProcessId.
// Required Connectors: MicrosoftThreatProtection (Types: DeviceNetworkEvents)

DeviceNetworkEvents
| where RemotePort == 445 and Timestamp > ago(7d) 
    // Exclude Kernel processes, as they are too noisy in this query
    and InitiatingProcessId !in (0, 4)
| summarize RemoteIPCount=dcount(RemoteIP) by DeviceName, InitiatingProcessFileName, InitiatingProcessId, InitiatingProcessCreationTime
| where RemoteIPCount > 10