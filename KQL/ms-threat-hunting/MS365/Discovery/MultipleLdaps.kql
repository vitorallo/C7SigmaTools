// Description: Detect multiple Active Directory LDAP queries made in bin time
// Replace 10 on line 1 with your desired thershold
// Replace 1m on line 2 with your desired bin time
// Required Connectors: MicrosoftThreatProtection (Types: IdentityQueryEvents)

let Thershold = 10;
let BinTime = 1m;
IdentityQueryEvents
| where ActionType == "LDAP query"
| parse Query with * "Search Scope: " SearchScope ", Base Object:" BaseObject ", Search Filter: " SearchFilter
| summarize NumberOfLdapQueries = count(), NumberOfDistinctLdapQueries = dcount(SearchFilter) by DeviceName, bin(Timestamp, BinTime)
| where NumberOfDistinctLdapQueries > Thershold