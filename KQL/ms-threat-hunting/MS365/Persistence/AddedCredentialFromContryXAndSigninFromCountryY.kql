// Description: Added credential from country X and Signed-In from country Y in a pecific time window:
// This query tries to find all applications that credentials were added to them from country X while the application's identity Signed-In from country Y in a specific time window.
// Required Connectors: MicrosoftThreatProtection (Types: CloudAppEvents, AADSpnSignInEventsBeta)

let timewindow = 1d;
let addedApps = (
CloudAppEvents
| where Application == "Office 365"
| where ActionType in ("Add service principal credentials.", "Update application - Certificates and secrets management ")
| project AddedTimestamp = Timestamp , AppName = tostring(RawEventData.Target[3].ID), CountryCode );
AADSpnSignInEventsBeta
| join addedApps on $left.ServicePrincipalName == $right.AppName
| where CountryCode != Country and AddedTimestamp + timewindow > Timestamp