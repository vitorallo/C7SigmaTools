// Description: Remote Monitoring and Management (RMM) programs are IT to manage remote endpoints. Attackers have begun to abuse these programs to persist or provide C2 channels.
// https://github.com/jischell-msft/RemoteManagementMonitoringTools
// Required Connectors: MicrosoftThreatProtection (Types: DeviceNetworkEvents)

let Time_start = now(-5d);
let Time_end = now();
//
DeviceNetworkEvents
| where Timestamp between (Time_start..Time_end)
| where RemoteUrl has_any (
        'rm.mspbackups.com',
        'client.rmm.mspbackups.com',
        'settings.services.mspbackups.com',
        'connect.ra.msp360.com',
        'foris.cloudberrylab.com'
    )
    and InitiatingProcessVersionInfoCompanyName has_any (
        'CloudBerry',
        'MSP360'
    )
| summarize FirstSeen=min(Timestamp), LastSeen=max(Timestamp), 
    Report=make_set(ReportId), Count=count() by DeviceId, DeviceName,
    RemoteUrl