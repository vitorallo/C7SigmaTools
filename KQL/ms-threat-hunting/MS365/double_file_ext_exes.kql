// Description: 'Provides a summary of executable files with double file extensions in SharePoint 
//  and the users and IP addresses that have accessed them.'
// Required Connectors: Office365 (Types: OfficeActivity (SharePoint))

let known_ext = dynamic(["lnk","log","option","config", "manifest", "partial"]);
let excluded_users = dynamic(["app@sharepoint"]);
OfficeActivity
| where RecordType =~ "SharePointFileOperation" and isnotempty(SourceFileName)
| where OfficeObjectId has ".exe." and SourceFileExtension !in~ (known_ext)
| extend Extension = extract("[^.]*.[^.]*$",0, OfficeObjectId)
| join kind= leftouter ( 
  OfficeActivity
    | where RecordType =~ "SharePointFileOperation" and (Operation =~ "FileDownloaded" or Operation =~ "FileAccessed") 
    | where SourceFileExtension !in~ (known_ext)
) on OfficeObjectId 
| where UserId1 !in~ (excluded_users)
| extend userBag = bag_pack(UserId1, ClientIP1) 
| summarize make_set(UserId1, 10000), make_bag(userBag), Start=max(TimeGenerated), End=min(TimeGenerated) by UserId, OfficeObjectId, SourceFileName, Extension 
| extend NumberOfUsers = array_length(bag_keys(bag_userBag))
| project UploadTime=Start, Uploader=UserId, FileLocation=OfficeObjectId, FileName=SourceFileName, AccessedBy=bag_userBag, Extension, NumberOfUsers
| extend UploaderName = tostring(split(Uploader, "@")[0]), UploaderUPNSuffix = tostring(split(Uploader, "@")[1])
| extend Account_0_Name = UploaderName
| extend Account_0_UPNSuffix = UploaderUPNSuffix